varnishtest "expired session cookie redirects to authorization endpoint"

server s_oidc -listen "127.0.0.1:18088" {
    rxreq
    expect req.url == "/.well-known/openid-configuration"
    txresp -status 200 -hdr "Content-Type: application/json" -body {
        {
            "issuer": "http://localhost",
            "authorization_endpoint": "http://127.0.0.1:18088/authorize",
            "token_endpoint": "http://127.0.0.1:18088/token",
            "jwks_uri": "http://127.0.0.1:18088/jwks"
        }
    }

    rxreq
    expect req.url == "/jwks"
    txresp -status 200 -hdr "Content-Type: application/json" -body {
        {
            "keys":[
                {"kty":"RSA","alg":"RS256","kid":"test-key","n":"AQAB","e":"AQAB"}
            ]
        }
    }
} -start

server s_app {
    rxreq
    txresp -status 200 -body "should-not-hit-backend"
} -start

varnish v1 -vcl+backend {
    import oidc from "${vmod}";

    sub vcl_init {
        new p = oidc.provider(
            discovery_url = "http://127.0.0.1:18088/.well-known/openid-configuration",
            client_id = "client-123",
            client_secret = "secret-xyz",
            redirect_uri = "http://127.0.0.1:8080/oidc/callback",
            cookie_secret = "0123456789abcdef0123456789abcdef0123456789abcdef0123456789abcdef",
            cookie_secure = false,
            scopes = "openid email profile"
        );
    }

    sub vcl_recv {
        set req.backend_hint = s_app;
        if (req.url ~ "^/protected/") {
            if (!p.session_valid()) {
                return (synth(302, "Login required"));
            }
        }
    }

    sub vcl_synth {
        if (resp.status == 302 && resp.reason == "Login required") {
            set resp.http.Location = p.authorization_url();
            return (deliver);
        }
    }
} -start

client c1 {
    txreq -url "/protected/resource" -hdr "Cookie: __oidc=v1.CwoJCAcGBQQDAgEA6zCCLoC1vuU8bS0tY4MnWNj-9duOxsGfAp5GrEdVdq-yZcQ2C1IwBRa_HuZHz6vpdlA324Eg9FpAh8V83nujSmxD1yOEISwrH0h8akX2VR1Mv07Ed9Fp5ObRzGOz1M4wnB0Y6CIlsK7hqRyR16E99dQ6Bz0lfRKUVaIxwMgOZmoEadn1LFR7Zr63wv-oGVXnoLQkwMY3vLzbFfF3RJZeiowyBsue6VcYrt8HbdOGRBxn-xc"
    rxresp
    expect resp.status == 302
    expect resp.http.Location ~ "^http://.*?/authorize\\?"
    expect resp.http.Location ~ "response_type=code"
    expect resp.http.Location ~ "client_id=client-123"
} -run
